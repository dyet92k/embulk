// TODO: Have more details in description and summary.
description = 'Embulk core'
ext {
    summary = 'Embulk core'
}

// include ruby scripts to jar. don't use sourceSets.main.resources.srcDirs
// because IntelliJ causes error if srcDirs includes files out of projectDir.
processResources.from("${buildDir}/embulk_gem_as_resources", "${buildDir}/dependency_gems_as_resources")

configurations {
    // com.google.inject:guice depends on asm and cglib but version of the libraries conflict
    // with ones bundled in jruby-complete and cause bytecode compatibility error
    api.exclude group: "asm", module: "asm"
    api.exclude group: "org.sonatype.sisu.inject", module: "cglib"
    compileClasspath.resolutionStrategy.activateDependencyLocking()
    runtimeClasspath.resolutionStrategy.activateDependencyLocking()
}

repositories {
    mavenCentral()
}

// determine which dependencies have updates: $ gradle dependencyUpdates
dependencies {
    // Embulk plugins have expected that embulk-core's dependencies are declared as "compile" in pom.xml.
    // Although those dependencies would be finally removed from embulk-core's direct dependencies,
    // those "compile" declaration would be kept so that plugin developers won't be confused unnecessarily.
    api project(":embulk-api")
    api project(":embulk-spi")
    api "com.google.guava:guava:18.0"
    api "com.google.inject:guice:4.0"
    api "com.google.inject.extensions:guice-multibindings:4.0"
    api "javax.inject:javax.inject:1"
    api "com.fasterxml.jackson.core:jackson-annotations:2.6.7"
    api "com.fasterxml.jackson.core:jackson-core:2.6.7"
    api "com.fasterxml.jackson.core:jackson-databind:2.6.7"
    api "com.fasterxml.jackson.datatype:jackson-datatype-guava:2.6.7"
    api "com.fasterxml.jackson.datatype:jackson-datatype-jdk8:2.6.7"
    api "com.fasterxml.jackson.datatype:jackson-datatype-joda:2.6.7"
    api "com.fasterxml.jackson.module:jackson-module-guice:2.6.7"
    api "org.slf4j:slf4j-api:1.7.12"
    api "org.jruby:jruby-complete:" + rootProject.jrubyVersion
    api "javax.validation:validation-api:1.1.0.Final"
    api "org.apache.bval:bval-jsr303:0.5"
    api "joda-time:joda-time:2.9.2"
    api "org.msgpack:msgpack-core:0.8.11"

    // bval-jsr303:0.5 depends on commons-lang3:3.1,
    // but it was upgraded to 3.4 when maven-aether-provider:3.3.9 was introduced.
    // Maven-related dependencies are moved to embulk-deps-maven.
    api "org.apache.commons:commons-lang3:3.4"

    testImplementation "junit:junit:4.12"
    testImplementation "ch.qos.logback:logback-classic:1.1.3"

    // TODO: Remove this, and load it with the proper DependencyClassLoader.
    // This statement gets it loaded by the top-level ClassLoader.
    testImplementation project(":embulk-deps-buffer")
    testImplementation project(":embulk-deps-config")
    testImplementation project(":embulk-deps-guess")
    testImplementation project(":embulk-deps-timestamp")
}

task rubyTestVanilla(type: JavaExec, dependsOn: [
        "jar",
        "prepareDependencyJars",
        "installDependencyGems",
        ":embulk-deps-buffer:prepareDependencyJars",
        ":embulk-deps-config:prepareDependencyJars",
        ":embulk-deps-guess:prepareDependencyJars",
        ":embulk-deps-timestamp:prepareDependencyJars",
        ]) {
    workingDir = file("${projectDir}");
    classpath = sourceSets.main.runtimeClasspath  // Not "configurations.runtimeClasspath" to use compiled embulk-core
    main = "org.jruby.Main"
    args = ["-Isrc/main/ruby", "-Isrc/test/ruby", "--debug", "./src/test/ruby/vanilla/run-test.rb"]
    environment "GEM_HOME": "${buildDir}/dependency_gems_installed"
}

task rubyTestMonkeyStrptime(type: JavaExec, dependsOn: ["jar"]) {
    workingDir = file("${project.projectDir}");
    classpath = sourceSets.main.runtimeClasspath  // Not "configurations.runtimeClasspath" to use compiled embulk-core
    main = "org.jruby.Main"
    args = ["-Isrc/main/ruby", "-Isrc/test/ruby", "--debug", "./src/test/ruby/monkey_strptime/run-test.rb"]
    environment "GEM_HOME": "${buildDir}/dependency_gems_installed"
}

test.dependsOn(['rubyTestVanilla', 'rubyTestMonkeyStrptime', ":test-helpers:deploy"])

String buildRubyStyleVersionFromJavaStyleVersion(String javaStyleVersion) {
    if (javaStyleVersion.contains('-')) {
        List<String> versionTokens = javaStyleVersion.tokenize('-');
        if (versionTokens.size() != 2) {
            throw new GradleException('Invalid project version: ' + javaStyleVersion);
        }
        return versionTokens.get(0) + '.' + versionTokens.get(1).toLowerCase();
    }
    else {
        return javaStyleVersion;
    }
}

task installDependencyGems(type: JavaExec) {
    doFirst {
        delete("${buildDir}/dependency_gems_installed")
        mkdir("${buildDir}/dependency_gems_installed")
    }

    workingDir = file("${projectDir}")
    classpath = configurations.runtimeClasspath  // Not "sourceSets.main.runtimeClasspath" not to depend on "jar"
    main = "org.jruby.Main"
    args = ["-S", "gem", "install", "msgpack:1.1.0", "bundler:1.16.0", "liquid:4.0.0", "simplecov:0.10.0", "test-unit:3.0.9"]
    environment "GEM_HOME": "${buildDir}/dependency_gems_installed"
}

task unpackDependencyGems(type: Copy, dependsOn: "installDependencyGems") {
    doFirst {
        delete("${buildDir}/dependency_gems_as_resources")
    }

    from "${buildDir}/dependency_gems_installed"
    into "${buildDir}/dependency_gems_as_resources"
    include "gems/bundler-1.16.0/**"
    include "gems/liquid-4.0.0/**"
    include "gems/msgpack-1.1.0-java/**"
    include "specifications/bundler-1.16.0.gemspec"
    include "specifications/liquid-4.0.0.gemspec"
    include "specifications/msgpack-1.1.0-java.gemspec"

    doLast {
        fileTree(dir: "${buildDir}/dependency_gems_as_resources", include: "**/.jrubydir").each { f -> f.delete() }
    }
}
processResources.dependsOn("unpackDependencyGems")

task buildEmbulkGem {
    doFirst {
        delete("${buildDir}/embulk_gem_as_resources")
    }
    doLast {
        def rubyVersion = buildRubyStyleVersionFromJavaStyleVersion("${project.version}")
        file("${buildDir}/embulk_gem_as_resources/gems/embulk-${rubyVersion}-java/lib").mkdirs()
        copy {
            from "${projectDir}/src/main/ruby"
            into "${buildDir}/embulk_gem_as_resources/gems/embulk-${rubyVersion}-java/lib"
        }
        file("${buildDir}/embulk_gem_as_resources/specifications").mkdirs()
        // TODO: Build the gemspec properly by the gem command, not by templating.
        // TODO: Set gem versions directly from this build.gradle's dependencies.
        // TODO: Fill the rubygems version from the actual JRuby runtime.
        // NOTE: add_development_dependency (test-unit and others) is basically not required here.
        file("${buildDir}/embulk_gem_as_resources/specifications/embulk-${rubyVersion}-java.gemspec").write($/\
# -*- encoding: utf-8 -*-
# stub: embulk ${rubyVersion} java lib

Gem::Specification.new do |s|
  s.name = "embulk".freeze
  s.version = "${rubyVersion}"
  s.platform = "java".freeze

  s.required_rubygems_version = Gem::Requirement.new(">= 0".freeze) if s.respond_to? :required_rubygems_version=
  s.require_paths = ["lib".freeze]
  s.authors = ["Sadayuki Furuhashi".freeze]
  s.date = "2017-11-22"
  s.description = "${project.description}".freeze
  s.email = ["frsyuki@gmail.com".freeze]
  s.executables = ["embulk".freeze]
  s.homepage = "https://github.com/embulk/embulk".freeze
  s.licenses = ["Apache 2.0".freeze]
  s.rubygems_version = "2.6.13".freeze
  s.summary = "${project.ext.summary}".freeze

  s.installed_by_version = "2.6.13" if s.respond_to? :installed_by_version

  if s.respond_to? :specification_version then
    s.specification_version = 4

    if Gem::Version.new(Gem::VERSION) >= Gem::Version.new('1.2.0') then
      s.add_runtime_dependency(%q<bundler>.freeze, [">= 1.10.6"])
      s.add_runtime_dependency(%q<msgpack>.freeze, ["~> 1.1.0"])
      s.add_runtime_dependency(%q<liquid>.freeze, ["~> 4.0.0"])
    else
      s.add_dependency(%q<bundler>.freeze, [">= 1.10.6"])
      s.add_dependency(%q<msgpack>.freeze, ["~> 1.1.0"])
      s.add_dependency(%q<liquid>.freeze, ["~> 4.0.0"])
    end
  else
    s.add_dependency(%q<bundler>.freeze, [">= 1.10.6"])
    s.add_dependency(%q<msgpack>.freeze, ["~> 1.1.0"])
    s.add_dependency(%q<liquid>.freeze, ["~> 4.0.0"])
  end
end
/$)
    }
}
processResources.dependsOn("buildEmbulkGem")

import java.util.zip.ZipFile
task updateResources(dependsOn: 'prepareDependencyJars') {
    doFirst {
        List<String> packages = []
        List<String> dirs = []
        file("${buildDir}/dependency_jars").listFiles().each { jarFile ->
            List<String> names = new ZipFile(jarFile.toString()).entries().iterator().collect { it.getName() }
            packages.addAll(names.findAll { it.endsWith(".class") }.collect { it.replaceFirst(/(?:^|\/)[^\/]*\.class$/, '').replaceAll(/\//, '.') }.unique())
            dirs.addAll(names.findAll { !it.endsWith("/") && !it.endsWith(".class") }.collect { it.replaceFirst(/(?:^|\/)[^\/]*$/, '') }.unique())
        }
        packages = packages.unique()
        dirs = dirs.unique()
        List<String> uniquePackages = packages.clone()
        List<String> uniqueDirs = dirs.clone()
        packages.each { pk -> uniquePackages.removeAll { it.startsWith(pk + '.') } }
        dirs.each { dir -> uniqueDirs.removeAll { it.startsWith(dir + '/') } }
        uniquePackages.removeAll { it.isEmpty() }
        uniqueDirs.removeAll { it.isEmpty() || it == "META-INF" }
        String date = new Date().format('yyyy-MM-dd HH:mm:ss z', TimeZone.getTimeZone("UTC"))
        file("src/main/resources/embulk/parent_first_packages.properties").withWriter { out ->
            out.println("# generated by './gradlew updateResources' at $date")
            uniquePackages.sort().each { out.println it }
        }
        file("src/main/resources/embulk/parent_first_resources.properties").withWriter { out ->
            out.println("# generated by './gradlew updateResources' at $date")
            uniqueDirs.sort().each { out.println it }
        }
    }
    doLast {
        prepareDependencyJars.execute()
    }
}

task prepareDependencyJars(type: Copy) {
    doFirst {
        delete file("${buildDir}/dependency_jars")
        mkdir file("${buildDir}/dependency_jars")
    }
    into "${buildDir}/dependency_jars"
    from configurations.runtimeClasspath
    from jar.outputs.files
}
